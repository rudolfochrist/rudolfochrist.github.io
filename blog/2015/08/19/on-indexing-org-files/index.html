<!DOCTYPE html>
<html lang="en-us">
  <head>
    <title>On indexing org files - Sebastian Christ</title>
    <meta charset="utf-8" />
    <meta name="author" content="Sebastian Christ" />
    <meta name="keywords" content="emacs, search, lisp" />
    <link rel="stylesheet" href="/media/css/main.css" type="text/css">
  </head>

  <body class="container">
    <div>
      <header class="masthead">
        <h1 class="masthead-title"><a href="/">Sebastian Christ</a></h1>
        <p>(funcall (Î» ()))</p>
        <ul>
          <li><a href="/blog/">Blog</a></li>
          <li><a href="/tags/">Tags</a></li>
          <li><a href="/about/">About</a></li>
          <li><a href="https://github.com/rudolfochrist">GitHub</a></li>
          <li><a href="/rss.xml">RSS</a></li>
        </ul>
        <form method="get" id="searchform" action="http://www.google.com/search">
          <input type="text" class="field" name="q" id="s" placeholder="Search">
          <input type="hidden" name="as_sitesearch" value="rudolfochrist.github.io">
        </form>
      </header>
    </div>

<div>
<div class="post">
<h1>On indexing org files</h1>
<p>
Since indexing org file is kind of <i>en vogue</i> lately, I thought that I should present my own home-brewed
solution. Firstly, I tried to use <a href="http://oremacs.com/2015/07/27/counsel-recoll/">recoll</a>, but unfortunately I was unable to compile it on OSX. Because I store
all of my notes in one huge org file, <a href="http://www.manpagez.com/man/1/mdfind/"><code>mdfind</code></a> wasn't a solution ether. <a href="http://kitchingroup.cheme.cmu.edu/blog/2015/07/06/Indexing-headlines-in-org-files-with-swish-e-with-laser-sharp-results/">John Kitchin</a> gave a lot of
inspiration, but somehow it didn't quite fit. So I had to hack something by myself...
</p>

<div id="outline-container-orgheadline1" class="outline-2">
<h2 id="orgheadline1">The indexer</h2>
<div class="outline-text-2" id="text-orgheadline1">
<p>
Because I think an all Lisp implementation is <i>sort-of</i> cool, I decided to write my indexer in Common Lisp
utilizing the Common Lisp <a href="https://lucene.apache.org/">Lucene</a> implementation majestically called <a href="http://www.cliki.net/Montezuma">Montezuma</a>.
</p>

<div class="org-src-container">

<pre class="src src-lisp">(defpackage :index-cli
  (:use :cl :net.didierverna.clon)
  (:import-from :montezuma
                :add-field
                :make-field)
  (:export
   #:main))

(in-package :index-cli)

(defvar *version* "0.1")
(defvar *index-path-name* ".org-file-index")
(defparameter *index-path* (merge-pathnames *index-path-name*))

(defun make-index (&amp;key create-p)
  (make-instance 'montezuma:index
                 :path *index-path*
                 :create-p create-p
                 :analyzer (make-instance 'montezuma:standard-analyzer)
                 :default-field "*"))

(defun search-index (query)
  (let ((index (make-index :create-p nil)))
    (when (= 0 (montezuma:search-each
                index
                query
                #'(lambda (doc-id score)
                    (declare (ignore score))
                    (format t "~A [~A|~A]~%"
                            (montezuma:document-value
                             (montezuma:get-document index doc-id)
                             'title)
                            (montezuma:document-value
                             (montezuma:get-document index doc-id)
                             'file)
                            (montezuma:document-value
                             (montezuma:get-document index doc-id)
                             'buffer-position)))
                '(:num-docs 100000)))
      (format t "Not found~%"))))

(defun index-file (filespec)
  (let ((all-docs (with-open-file (f filespec)
                    (read f)))
        (index (make-index :create-p t)))
    (dolist (doc all-docs)
    (montezuma:add-document-to-index index doc
       (make-instance 'montezuma:standard-analyzer)))
    (montezuma:optimize index)
    (montezuma:close index)))

;;; CLI

(defsynopsis (:postfix "FILE")
  (text :contents "Index alist file")
  (path :long-name "index-path" :short-name "i" :type :directory
        :description "Index location. Defaults to current directory.")
  (stropt :long-name "query" :short-name "q"
          :description "Search terms.")
  (flag :long-name "help" :short-name "h"
        :description "Show this help text.")
  (flag :long-name "version" :short-name "v"
        :description "Show the version."))

(defun main (args)
  (setf sb-impl::*default-external-format* :utf-8)
  (make-context :cmdline args)
  (when (getopt :short-name "h")
    (help)
    (sb-ext:exit))
  (when (getopt :short-name "v")
    (format t "index-cli v~A~%" *version*)
    (sb-ext:exit))
  (let ((index-path (getopt :short-name "i"))
        (query (getopt :short-name "q")))
    (when index-path
      (setf *index-path* (merge-pathnames *index-path-name* index-path)))
    (if query
        (search-index query)
        (index-file (merge-pathnames (car (remainder)))))))
</pre>
</div>

<p>
The most important points are the <code>index-file</code> and the <code>search-index</code> functions. The file passed to
<code>index-file</code> should contain a list of alist. It iterates over this list and passes each alist to
Montezuma. Simple as that. The alists contains the following data:
</p>

<dl class="org-dl">
<dt><b>title</b></dt><dd>Usually the org headline.</dd>
<dt><b>contents</b></dt><dd>A paragraph's text. Meaning the text in between two org headlines.</dd>
<dt><b>file</b></dt><dd>The absolute file path to the file where <b>title</b> or <b>contents</b> has been extracted.</dd>
<dt><b>buffer-position</b></dt><dd>The position of the headline or paragraph in the buffer. Knowing that, it is easy to
jump directly to the occurrence of the search term. This will be important later.</dd>
</dl>

<p>
Example:
</p>

<div class="org-src-container">

<pre class="src src-lisp">((title . "An awesome title")
 (contents . "Much much content...")
 (file . "/Users/lispm/org/kb.org")
 (buffer-position . 311518))
</pre>
</div>

<p>
Each alist is a document in Lucene/Montezuma lingo. Note that <b>contents</b> is optional actually. This is because
I'm indexing headlines and paragraphs independently. But when I index a paragraph I put it's parent-headline
into the document as well. Admittedly this indexes the same headline multiple times which is
unnecessary actually, but I'm fine with that.
</p>

<p>
The <code>search-index</code> function applies the given query to the index an is explained quickly. If documents for
this query are found, each document gets printed to <b>standard-output</b>. For example, querying for <code>awesome title</code> writes
the above document like the following:
</p>

<pre class="example">
An awesome title [/Users/lispm/org/kb.org|311518]
</pre>

<p>
This output format still applies if the search term has been found inside the <b>contents</b> of a document. Thus
I've indexed the headline for paragraphs as well. To finalize the indexer, I've created an  executable with <a href="http://www.xach.com/lisp/buildapp/">buildapp</a>
and gave it a primitive CLI. The <b>-i</b> option
tells it where the index should be stored. The <b>-q</b> option searches the index. Omitting <b>-q</b> and passing the
documents file (the list of alists) indexes it.
</p>
</div>
</div>

<div id="outline-container-orgheadline2" class="outline-2">
<h2 id="orgheadline2">The documents creator</h2>
<div class="outline-text-2" id="text-orgheadline2">
<p>
Next I needed a script that transforms my org file into a list of indexable documents.
</p>

<div class="org-src-container">

<pre class="src src-emacs-lisp">;;; use the latest org
(add-to-list 'load-path "/Users/fyi/.emacs.d/elpa/org-20150727/")
(require 'org)
(require 'xml)
(require 'json)

(defun parent-headline (element)
  (let ((parent (org-element-property :parent element)))
    (cond
      ((null parent)
       "")
      ((and (eq 'headline (org-element-type parent))
            (eql 1 (org-element-property :level parent)))
       (org-element-property :raw-value parent))
      (t
       (parent-headline parent)))))

(find-file (expand-file-name (car command-line-args-left)))

(princ (append
        (org-map-entries
         (lambda ()
           (let* ((headline (org-element-at-point))
                  (title (org-element-property :title headline)))
             (if (equal title "Website Summary:")
                 ""
                 (format "((title . %S) (file . %S) (buffer-position . %s))"
                         title
                         (buffer-file-name)
                         (prin1-to-string
                          (org-element-property :begin headline)))))))
        (org-element-map (org-element-parse-buffer) 'paragraph
          (lambda (paragraph)
            (format
             "((title . %S) (contents . %S) (file . %S) (buffer-position . %s))"
             (parent-headline paragraph)
             (xml-escape-string
              (buffer-substring-no-properties
               (org-element-property :contents-begin paragraph)
               (org-element-property :contents-end paragraph)))
             (buffer-file-name)
             (prin1-to-string (org-element-property :begin paragraph)))))))
</pre>
</div>

<p>
The above script takes an org file and maps over each headline and paragraph an produces an alist
accordingly. Nothing special about it. I didn't know about the <code>org-element</code>-API before, which is quite nice.
</p>
</div>
</div>

<div id="outline-container-orgheadline3" class="outline-2">
<h2 id="orgheadline3">Swiper integration</h2>
<div class="outline-text-2" id="text-orgheadline3">
<p>
Lastly, everything get swiper-integrated to be usable from inside Emacs.
</p>

<div class="org-src-container">

<pre class="src src-emacs-lisp">(defun counsel-kb-search-function (string &amp;rest unused)
  "Lookup STRING with index-cli"
  (if (&lt; (length string) 3)
      (counsel-more-chars 3)
      (counsel--async-command
       (format "index-cli -i /Users/lispm/org -q \"%s\"" string))
      nil))

(defun search-kb (&amp;optional initial-input)
  "Search KB"
  (interactive)
  (ivy-read "search KB: " 'counsel-kb-search-function
            :initial-input initial-input
            :dynamic-collection t
            :history 'counsel-git-grep-history
            :action
            (lambda (x)
              (when (string-match ".*\\[\\(.*\\)|\\([[:digit:]]+\\)\\]" x)
                (let ((file-name (match-string 1 x))
                      (point (string-to-number (match-string 2 x))))
                  (find-file file-name)
                  (goto-char point)
                  (org-show-entry)
                  (show-children))))))
</pre>
</div>

<p>
The regexp matches the output of the indexer. Because I've indexed the file path and the buffer position I can jump
directly to the search term from swiper. Which is exactly what I wanted.  See a demo below:
</p>


<div class="figure">
<p><img src="/assets/blog/2015/08/19/on-indexing-org-files/index-cli-demo.gif" alt="index-cli demo" title="index-cli demo" width="700px" />
</p>
</div>
</div>
</div>

</div>
</div>
    <div>
      <div class="post-meta">
        <span title="post date" class="post-info">2015-08-19</span>
        <span title="last modification date" class="post-info">2015-08-19</span>
        <span title="tags" class="post-info"><a href="/tags/emacs/">emacs</a>, <a href="/tags/search/">search</a>, <a href="/tags/lisp/">lisp</a></span>
        <span title="author" class="post-info">Sebastian Christ</span>
      </div>
      <script src="http://code.jquery.com/jquery-latest.min.js"></script>
      <script src="https://cdnjs.cloudflare.com/ajax/libs/prettify/r298/prettify.js"></script>
      <script src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML"></script>
      <script src="/media/js/main.js"></script>
      <div class="footer">
        <p>Generated by <a href="http://www.gnu.org/software/emacs/">Emacs</a> 24.x (<a href="http://orgmode.org">Org mode</a> 8.x)</p>
        <p>
          Copyright &copy; 2012 - <span id="footerYear"></span> <a href="mailto:rudolfo &lt;dot&gt; christ &lt;at&gt; gmail &lt;dot&gt; com">Sebastian Christ</a>
          &nbsp;&nbsp;-&nbsp;&nbsp;
          Powered by <a href="https://github.com/kelvinh/org-page" target="_blank">org-page</a>
          <script type="text/javascript">document.getElementById("footerYear").innerHTML = (new Date()).getFullYear();</script>
        </p>
      </div>
    </div>

  </body>
</html>
